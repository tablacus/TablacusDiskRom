
;	Tablacus DISK ROM - INIT
;

INIT_ROM:
	LD	HL,AT_ISC
	LD	DE,ISC
	LD	BC,ISC_E-ISC
	LDIR

	LD	HL,ISC
	LD	(0F390H),HL

	CALL	GTSL1_
	LD	(ROM_SLT),A
				;ROMタイプ判別(ASCII-8K/ASCII-16K)
	LD	A,01FH
	LD	(ROM_MODE),A
	LD	HL,BANK2_SEL
	LD	(ROM8000H),HL

	LD	E,0
	LD	A,(ROM_SLT)
	LD	HL,BANK1_SEL
	CALL	_WRSLT

	LD	HL,06000H
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	CP	'A'
	JR	Z,ROM8K
				;ASCII-8K/Konami-8Kではない(ASCII-16K)
	LD	A,03FH
	LD	(ROM_MODE),A
	JR	ROMCHECKED
ROM8K:				;Konami-8Kチェック
	LD	E,1
	LD	A,(ROM_SLT)
	LD	HL,BANK2_SEL
	CALL	_WRSLT

	LD	E,0
	LD	A,(ROM_SLT)
	LD	HL,KONAMI_BANK2_SEL
	CALL	_WRSLT

	LD	HL,08000H
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	CP	'A'
	JR	NZ,ROMCHECKED
				;Konami-8K
	LD	HL,KONAMI_BANK2_SEL
	LD	(ROM8000H),HL
ROMCHECKED:
	RET

GTSL1_:
	PUSH	HL		;Save registers
	PUSH	DE
;
	CALL	RSLREG		;read primary slot #
	RRCA
	RRCA
	AND	11B		;[A]=000000PP
	LD	E,A
	LD	D,0		;[DE]=000000PP
	LD	HL,EXPTBL
	ADD	HL,DE		;[HL]=EXPTBL+000000PP
	LD	E,A		;[E]=000000PP
	LD	A,(HL)		;[A]=(EXPTBL+000000PP)
	AND	80H		;Use only MSB
	JR	Z,GTSL1NOEXP
	OR	E		;[A]=F00000PP
	LD	E,A		;save primary slot number
	INC	HL		;point to SLTTBL entry
	INC	HL
	INC	HL
	INC	HL
	LD	A,(HL)		;get currently expansion slot register
	RRCA
	RRCA
	AND	11B		;[A] = 000000SS
	RLCA
	RLCA			;[A] = 0000SS00
	OR	E		;[A] = F000SSPP
;
GTSL1END:
	POP	DE
	POP	HL
	RET
GTSL1NOEXP:
	LD	A,E		;[A] = 000000PP
	JR	GTSL1END

SAVE_ADR:
	DB	03EH	;LD A,??
	RST	30H
	DI
	LD	(HL),A
	CALL	GTSL1_
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	EI
	RET

AT_ISC:
	ORG ISC,AT_ISC-RUN
	PUSH	AF
	LD	A,IXH
	CP	040H
	JR	NZ,ISC1

	LD	A,IXL
	CP	010H
	JR	C,ISC1

	CP	020H
	JR	NC,ISC1

	EX	AF,AF'
	PUSH	DE
	LD	E,A
	AND	00CH
	CP	00CH
SWC1	EQU	$-1
	LD	A,E
	POP	DE
	JR	Z,ISC2
	EX	AF,AF'
ISC1:
	POP	AF
ISC3:
	JP	(IX)
ISC2:
	EX	AF,AF'
	POP	AF
	LD	IYH,0
ROM_SLT	EQU	$-1
	CALL	_CALSLT
	RET
ROM_MODE:
	DB	0
ROM8000H:
	DW	0

ISC_E:
	ORG $$+RUN			;$DEPHASE


