
;	Tablacus DISK ROM - INIT
;

INIT_ROM:
	LD	A,(CLPRIM+4)	;一時的にインタースロットコールをフックしてる？
	CP	low ISC
	RET	Z

	LD	HL,AT_ISC
	LD	DE,ISC
	LD	BC,ISC_E-ISC
	LDIR

	CALL	GTSL1_
	LD	(ROM_SLT),A

	XOR	A
	LD	(BANK0_SEL),A			;似非RAM対策でバンク0を設定
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	A,(BANK1_ADR)
	CP	'A'
	JR	NZ,NOT_8K_BANK
	LD	A,DISK_BANK
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC

	LD	HL,(BANK1_ADR+19)	;BPB_TotSec16	
	LD	BC,0000FH		;切り上げ
	ADD	HL,BC
	LD	A,L

	LD	B,4
B_DRIVE1:
	SRL	H
	RRA
	DJNZ	B_DRIVE1

	ADD	A,DISK_BANK		;A=(TotSec16/16)+DISK_BANK
	LD	(B_DRIVE),A

	XOR	A
	CALL	IS_BPB
	JR	C,DSKCHG1
	LD	A,1
	CALL	IS_BPB
	JR	NC,DSKCHG1
	LD	A,0FFH
	LD	(B_DRIVE),A
DSKCHG1:

	LD	HL,ISC
	LD	(CLPRIM+4),HL	;一時的にインタースロットコールをフックする
	LD	IX,INIT1
	JP	ISC
INIT1:
	RET
NOT_8K_BANK:
				;ASCII-8K/Konami-8Kではない(ASCII-16K等)
	LD	HL,MSG_ERROR_ROM_MODE
	CALL	MSX
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	LD	IX,CHGET
	JP	_CALSLT

GTSL1_:				;自分のいるスロットを知る
	CALL	RSLREG		;read primary slot #
	RRCA
	RRCA
	AND	3		;[A]=000000PP
	LD	E,A		;[E]=000000PP
	LD	HL,EXPTBL
	ADD	A,L		;桁上りは無い
	LD	L,A		;[HL]=EXPTBL+000000PP
	LD	A,E		;[A]=000000PP
	BIT	7,(HL)
	RET	Z
	LD	A,L		;point to SLTTBL entry
	ADD	A,4		;桁上りは無い
	LD	L,A
	LD	A,(HL)		;get currently expansion slot register
	AND	00CH		;[A] = 0000SS00
	OR	E		;[A] = 0000SSPP
	OR	080H		;[A] = 1000SSPP
	RET

AT_ISC:
	ORG	ISC,AT_ISC-RUN
	PUSH	AF
	PUSH	HL
	LD	HL,DRVTBL+1
	LD	A,(HL)
	OR	A		;DRVTBLのスロットが使われるまで待つ
	JR	Z,ISC_DEF
	LD	A,(ROM_SLT)	;DRVTBLのスロットをTablacus DISK ROMに置き換える
	LD	(HL),A
	LD	HL,CLPRIM+12	;インタースロットコールのフックを元に戻す
	LD	(CLPRIM+4),HL
ISC_DEF:
	POP	HL
	POP	AF
	JP	(IX)

	DS	2
ROM_SLT:
	DB	0
B_DRIVE:
	DB	0
SECSZ:				;セクタサイズ
	DW	512
SECSZ_H	EQU	$-1		;セクタサイズ上位8ビット
ISC_E:
	ORG	$$+RUN			;$DEPHASE

MSX1:
	CALL	MSGA1
MSX:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	NZ,MSX1
	RET

PRTHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRTA2
	POP	AF
PRTA2:
	CALL	ASC
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	IY,(EXPTBL-1)	;メインROMスロット
	LD	IX,CHPUT
	CALL	_CALSLT
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
ASC:
	AND	$0F
	OR	$30
	CP	$3A
	RET	C
	ADD	A,7
	RET
	
MSG_ERROR_ROM_MODE:
	DB	"ASCII-8K/Konami-8K/Konami-SCC only!",13,10,0
MSG_ERROR_DRIVE:
	DB	"Illegal DRIVE:",0
MSG_CRLF:
	DB	13,10,0
