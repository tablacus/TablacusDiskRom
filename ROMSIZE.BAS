10 CLEAR 1000,&HB000
20 BLOAD "ROMSIZE.BIN",R
30 M=&HB100:T=0:IF PEEK(M) = 0 THEN END
40 PRINT "Disk name:         ";
50 FOR I=0 TO 7:PRINT CHR$(PEEK(M+3+I));:NEXT:PRINT 'BS_OEMName
60 PRINT "Tablacus DISK ROM:";16384
80 BS=PEEK(M+11)+PEEK(M+12)*256 'BPB_BytePerSec
90 SC=PEEK(M+13) 'BPB_SecPerClus
100 RC=PEEK(M+14)+PEEK(M+15)*256 'BPB_RsvdSecCnt
110 I=RC*BS:T=T+I
115 FA=M+I+3 'FAT ADDRESS
120 PRINT "Reserved:         ";I
130 NF=PEEK(M+16) 'BPB_NumFATs
140 FS=PEEK(M+22)+PEEK(M+23)*256 'BPB_FATSz16
150 I=NF*FS*BS:T=T+I
160 PRINT "FAT:              ";I
170 RE=PEEK(M+17)+PEEK(M+18)*256 'BPB_RootEntCnt
180 I=RE*32:T=T+I
190 PRINT "Directory:        ";I
200 TS=PEEK(M+19)+PEEK(M+20)*256 'BPB_TotSec16
210 I=TS*BS-T
220 CL=I/SC/BS 'Clusters
230 'PRINT "Clusters:";CL
240 UC=-1
250 FOR I=CL-1 TO 0 STEP -1
260 IF I MOD 2 <> 0 THEN F=PEEK(FA+I*1.5)+(PEEK(FA+I*1.5+1) AND 15)*256 ELSE F=(PEEK(FA+I*1.5-0.5) AND &HF0)/16+PEEK(FA+I*1.5+0.5)*16
270 IF F <> 0 THEN UC=I:I=0
280 NEXT
290 I=(UC+1)*BS*SC:T=T+I+16384
300 PRINT "Data:             ";I
310 PRINT "Total:            ";T
320 PRINT "Total(KB):        ";(T/1024);"KB"
