
;	Tablacus DISK ROM - DISK

;	ROM DISK DRIVER
;	1セクタ512

ERROR_WRITE_PROTECTED:
	LD	A,0		;Write protected
	RET
DISKIO:
	JR	C,ERROR_WRITE_PROTECTED
	LD	C,B
	PUSH	HL
DISKIO1:
	PUSH	BC
	PUSH	DE

	PUSH	HL
	EX	DE,HL
	ADD	HL,HL
	PUSH	HL	;あとでDEで受け取る
	ADD	HL,HL	;64KB→32KB
	ADD	HL,HL	;32KB→16KB

	LD	A,(ROM_MODE)
	AND	020H
	LD	A,1			;16KB単位でカートリッジ内のディスクが存在する位置を指定
	JR	NZ,ASCII16K1
	ADD	HL,HL	;16KB→8KB	;ASCII-16Kの場合はこの処理を飛ばす
	ADD	A,A
ASCII16K1:
	ADD	A,H
	LD	E,A
	LD	HL,(ROM8000H)
	LD	A,(ROM_SLT)
	CALL	_WRSLT

	POP	DE
	POP	HL		;HLは読み出し先のメモリアドレス

	LD	A,(ROM_MODE)	;セグメントのサイズでフィルタする(8K:1F/16K:3F)
	AND	E
	LD	E,0		;DEはROMディスクから読み出すアドレス
	ADD	A,080H
	LD	D,A

	LD	BC,00200H	;BCは読み出すセクタサイズ

	EX	DE,HL
DISKIO2:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	HL
	POP	DE
	POP	BC
	LD	(DE),A
	INC	DE
	CPI			;HL=HL+1 BC=BC-1 BCが0の場合にP/V=0
	JP	PE,DISKIO2

	EX	DE,HL
	POP	DE
	INC	DE
	POP	BC
	DJNZ	DISKIO1
	LD	B,C

	POP	HL

	XOR	A
	EI
	RET

DSKCHG:
	XOR	A
	LD	B,1
	RET

GETDPB:
	INC	HL
	LD	DE,DPB2DD
	LD	BC,DPB2DD_E-DPB2DD
	LDIR
	RET

CHOICE:
	LD	HL,0
	RET

DSKFMT:
	XOR	A
	SCF
DSKSTP:
	RET


DPB2DD:
	DB	0F9H	;MEDIA
	DW	00200H	;SECSIZ
	DB	00FH	;DIRMSK
	DB	004H	;DIRSHFT
	DB	001H	;CLUSMSK
	DB	002H	;CLUSSHFT
	DW	00001H	;FIRFAT
	DB	002H	;FATCNT
	DB	070H	;MAXENT
	DW	0000EH	;FIRREC
	DW	002CAH	;MAXCLUS
	DB	003H	;FATSIZ
	DW	0007H	;FIRDIR
DPB2DD_E:
