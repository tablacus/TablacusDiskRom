
;	Tablacus DISK ROM - DISK

;	ROM DISK DRIVER
;	1セクタ512

ERROR_WRITE_PROTECTED:
	LD	A,0		;Write protected
	RET
DISKIO:
	JR	C,ERROR_WRITE_PROTECTED
	LD	C,B
	PUSH	HL
DISKIO1:
	PUSH	BC
	PUSH	DE
	PUSH	AF		;A=DRIVE

	PUSH	HL
	CALL	MAPPING
	POP	HL		;HLは読み出し先のメモリアドレス

	LD	A,(ROM_MODE)	;セグメントのサイズでフィルタする(8K:1F/16K:3F)
	AND	E
	LD	E,0		;DEはROMディスクから読み出すアドレス
	ADD	A,080H
	LD	D,A

	LD	BC,00200H	;BCは読み出すセクタサイズ

	EX	DE,HL
DISKIO2:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	HL
	POP	DE
	POP	BC
	LD	(DE),A
	INC	DE
	CPI			;HL=HL+1 BC=BC-1 BCが0の場合にP/V=0
	JP	PE,DISKIO2

	EX	DE,HL
	POP	AF
	POP	DE
	INC	DE
	POP	BC
	DJNZ	DISKIO1
	LD	B,C

	POP	HL

	XOR	A
	EI
	RET

DSKCHG:
	LD	DE,0
	CALL	MAPPING

	LD	HL,08000H+21		;BPB_Media	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	OR	A
	JR	Z,NOTREADY
	XOR	A
	LD	B,1
	RET
NOTREADY:
	LD	A,2
	SCF
	RET

NO_BPB:
	POP	HL
	INC	HL
	LD	DE,DPB2DD
	LD	BC,DPB2DD_E-DPB2DD
	LDIR
	XOR	A
	RET

GETDPB:
	PUSH	HL
	LD	DE,0
	CALL	MAPPING

	LD	HL,08000H+21		;BPB_Media	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	OR	A
	JR	Z,NO_BPB
	POP	IX
	LD	(IX+1),A		;MEDIA

	PUSH	IX
	LD	HL,08000H+11		;BPB_BytsPerSec	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+2),A		;SECSIZ LOW

	PUSH	IX
	LD	HL,08000H+12		;BPB_BytsPerSec	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+3),A		;SECSIZ HIGH

	ADD	A,A
	ADD	A,A
	ADD	A,A
	DEC	A
	LD	(IX+4),A		;DIRMSK

	LD	B,-1
	AND	A			;無限ループ阻止
GETDPB1:
	INC	B
	RRA
	JR	C,GETDPB1
	LD	(IX+5),B		;DIRSHFT

	PUSH	IX
	LD	HL,08000H+13		;BPB_SecPerClus	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	DEC	A
	LD	(IX+6),A		;CLUSMSK

	AND	A			;無限ループ阻止
	LD	B,-1
GETDPB2:
	INC	B
	RRA
	JR	C,GETDPB2
	INC	B
	LD	(IX+7),B		;CLUSSHFT

	PUSH	IX
	LD	HL,08000H+14		;BPB_RsvdSecCnt	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+8),A		;FIRFAT LOW

	PUSH	IX
	LD	HL,08000H+15		;BPB_RsvdSecCnt	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+9),A		;FIRFAT HIGH

	PUSH	IX
	LD	HL,08000H+16		;BPB_NumFATs	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+10),A		;FATCNT

	PUSH	IX
	LD	HL,08000H+17		;BPB_RootEntCnt	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+11),A		;FATCNT

	PUSH	IX
	LD	HL,08000H+22		;BPB_FATSz16	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	IX
	LD	(IX+16),A		;FATSIZ

	LD	L,A			;FATSIZ
	LD	H,0
	LD	A,(IX+10)		;FATCNT
GETDPB3:
	DEC	A
	JR	Z,GETDPB4
	ADD	HL,HL
	JR	GETDPB3
GETDPB4:
	LD	C,(IX+8)		;FIRFAT
	LD	B,(IX+9)		;FIRFAT
	ADD	HL,BC
	LD	(IX+17),L		;FIRDIR
	LD	(IX+18),H		;FIRDIR

	LD	A,(IX+3)		;SECSIZ HIGH
	LD	E,(IX+11)		;MAXENT
	SRL	E
	SRL	E
GETDPB5:
	SRL	E
	SCF				;無限ループ阻止
	RRA
	JR	NC,GETDPB5
	LD	D,0
	ADD	HL,DE
	LD	(IX+12),L		;FIRREC
	LD	(IX+13),H		;FIRREC

	PUSH	IX
	LD	HL,08000H+20		;BPB_TotSec16	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	PUSH	AF
	LD	HL,08000H+19		;BPB_TotSec16	
	LD	A,(ROM_SLT)
	CALL	_RDSLT
	POP	HL
	LD	L,A			;HL=TotSec16
	POP	IX

	LD	C,(IX+12)		;FIRREC
	LD	B,(IX+13)		;FIRREC
	AND	A
	SBC	HL,BC

	LD	A,(IX+6)		;CLUSMSK
	INC	A
	SCF				;無限ループ阻止
GETDPB6:
	RRA
	JR	C,GETDPB7
	SRL	H
	RR	L
	JR	GETDPB6
GETDPB7:
	INC	HL
	LD	(IX+14),L		;MAXCLUS
	LD	(IX+15),H		;MAXCLUS
	XOR	A
	RET

CHOICE:
	LD	HL,0
	RET

DSKFMT:
	XOR	A
	SCF
DSKSTP:
	RET

MAPPING:
	EX	DE,HL
	ADD	HL,HL
	PUSH	HL	;あとでDEで受け取る
	ADD	HL,HL	;64KB→32KB
	ADD	HL,HL	;32KB→16KB

	PUSH	BC
	INC	A
	LD	B,A
	LD	A,(ROM_MODE)
	AND	020H
	LD	C,A
	LD	A,-44			;16KB単位でカートリッジ内のディスクが存在する位置を指定
SETMAP1:
	ADD	A,45			;720KB/16KB
	DJNZ	SETMAP1
	INC	C
	DEC	C
	POP	BC
	JR	NZ,ASCII16K1
	ADD	HL,HL	;16KB→8KB	;ASCII-16Kの場合はこの処理を飛ばす
	ADD	A,A
ASCII16K1:
	ADD	A,H
	LD	E,A
	LD	A,(ROM8000H_H)
	LD	H,A
	LD	A,(ROM_SLT)
	CALL	_WRSLT

	POP	DE
	RET

DPB2DD:
	DB	0F9H	;MEDIA
	DW	00200H	;SECSIZ
	DB	00FH	;DIRMSK
	DB	004H	;DIRSHFT
	DB	001H	;CLUSMSK
	DB	002H	;CLUSSHFT
	DW	00001H	;FIRFAT
	DB	002H	;FATCNT
	DB	070H	;MAXENT
	DW	0000EH	;FIRREC
	DW	002CAH	;MAXCLUS
	DB	003H	;FATSIZ
	DW	0007H	;FIRDIR
DPB2DD_E:
