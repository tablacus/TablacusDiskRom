
;	Tablacus DISK ROM - DISK

;	ROM DISK DRIVER
;	1セクタ512

ERROR_WRITE_PROTECTED:
	LD	A,0		;Write protected
	RET
DISKIO:
	JR	C,ERROR_WRITE_PROTECTED
	LD	C,B
	CALL	GET_DISK_BANK
SETMAP1:		
	PUSH	HL
DISKIO1:
	PUSH	BC		;B=残りのセクタ数
	PUSH	DE		;DE=セクタ番号
	PUSH	AF		;A=8KB単位でカートリッジ内のディスクが存在する位置を指定

	EX	DE,HL		;DE=受け取る読み出し先のメモリアドレス HL=セクタ番号
	ADD	HL,HL
	LD	C,L		;C=読み出し元(セクタ番号*2)※1セクタ512バイトなので倍にしておく
	ADD	HL,HL		;64KB→32KB
	ADD	HL,HL		;32KB→16KB
	ADD	HL,HL		;16KB→8KB
	ADD	A,H
	LD	(BANK1_SEL),A	;ASCII-8K/Konami-8K共通で使える
	LD	A,C		;C=読み出し元
	AND	01FH		;セグメントのサイズでフィルタする(8K:1F/16K:3F)
	ADD	A,high BANK1_ADR
	LD	H,A
	LD	BC,00200H	;BCは読み出すセクタサイズ(512バイト)
	LD	L,C		;C=0
	LDIR
	EX	DE,HL
	POP	AF
	POP	DE
	INC	DE
	POP	BC
	DJNZ	DISKIO1
	LD	B,C

	POP	HL
	XOR	A
	EI
	RET

DSKCHG:
	CALL	IS_BPB
	JR	C,NOTREADY
	LD	A,(DRVTBL+2)
	OR	A
	JR	NZ,DSKCHG1
	LD	A,(DRVTBL)
	CP	2
	JR	NZ,DSKCHG1
	CALL	IS_BPB
	JR	NC,DSKCHG1
	LD	A,1			;ドライブテーブルを拡張 A:Tablacus DISK ROM B:MASTER SLOT ROM
	LD	(DRVTBL),A
	LD	(DRVTBL+2),A
	LD	A,(MASTERS)
	LD	(DRVTBL+3),A
DSKCHG1:
	XOR	A
	LD	B,1
	RET
NOTREADY:
	LD	A,2
	SCF
	RET

NO_BPB:
	POP	HL
	INC	HL
	LD	DE,DPB2DD
	LD	BC,DPB2DD_E-DPB2DD
	LDIR
	XOR	A
	RET

IS_BPB:
; in
; A=DRIVE 0:A 1:B
; out
; CF=0ディスクは存在する CF=1 ディスクは存在しない
	CALL	GET_DISK_BANK
	LD	(BANK1_SEL),A

	LD	A,(BANK1_ADR+21)	;BPB_Media
	CP	1
	RET	C

	LD	A,(BANK1_ADR+11)	;BPB_BytsPerSec
	ADD	A,0FFH
	RET	C

	LD	A,(BANK1_ADR+12)	;BPB_BytsPerSec
	CP	2
	RET	Z
	CP	4
	RET	Z
	SCF
	RET

GETDPB:
	PUSH	HL
	CALL	GET_DISK_BANK
	LD	(BANK1_SEL),A

	LD	A,(BANK1_ADR+21)	;BPB_Media	
	OR	A
	JR	Z,NO_BPB
	POP	IX
	LD	(IX+1),A		;MEDIA

	LD	A,(BANK1_ADR+11)	;BPB_BytsPerSec	
	LD	(IX+2),A		;SECSIZ LOW

	LD	A,(BANK1_ADR+12)	;BPB_BytsPerSec	
	LD	(IX+3),A		;SECSIZ HIGH

	ADD	A,A
	ADD	A,A
	ADD	A,A
	DEC	A
	LD	(IX+4),A		;DIRMSK

	LD	B,-1
	AND	A			;無限ループ阻止
GETDPB1:
	INC	B
	RRA
	JR	C,GETDPB1
	LD	(IX+5),B		;DIRSHFT

	LD	A,(BANK1_ADR+13)	;BPB_SecPerClus	
	DEC	A
	LD	(IX+6),A		;CLUSMSK

	AND	A			;無限ループ阻止
	LD	B,-1
GETDPB2:
	INC	B
	RRA
	JR	C,GETDPB2
	INC	B
	LD	(IX+7),B		;CLUSSHFT

	LD	HL,(BANK1_ADR+14)	;BPB_RsvdSecCnt	
	LD	(IX+8),L		;FIRFAT LOW
	LD	(IX+9),H		;FIRFAT HIGH

	LD	A,(BANK1_ADR+16)	;BPB_NumFATs	
	LD	(IX+10),A		;FATCNT

	LD	A,(BANK1_ADR+17)	;BPB_RootEntCnt	
	LD	(IX+11),A		;FATCNT

	LD	DE,(BANK1_ADR+22)	;BPB_FATSz16	
	LD	(IX+16),E		;FATSIZ

	LD	B,(IX+10)		;FATCNT
	LD	HL,0
GETDPB3:
	ADD	HL,DE
	DJNZ	GETDPB3
GETDPB4:
	LD	E,(IX+8)		;FIRFAT
	LD	D,(IX+9)		;FIRFAT
	ADD	HL,DE
	LD	(IX+17),L		;FIRDIR
	LD	(IX+18),H		;FIRDIR

	LD	A,2*4			;SECSIZ HIGH
	LD	E,(IX+11)		;MAXENT
GETDPB5:
	SRL	E
	RRA
	JR	NC,GETDPB5
	LD	D,0
	ADD	HL,DE
	LD	(IX+12),L		;FIRREC
	LD	(IX+13),H		;FIRREC

	LD	HL,(BANK1_ADR+19)	;BPB_TotSec16	

	LD	E,(IX+12)		;FIRREC
	LD	D,(IX+13)		;FIRREC
	AND	A
	SBC	HL,DE

	LD	A,(IX+6)		;CLUSMSK
	INC	A
	SCF				;無限ループ阻止
GETDPB6:
	RRA
	JR	C,GETDPB7
	SRL	H
	RR	L
	JR	GETDPB6
GETDPB7:
	INC	HL
	LD	(IX+14),L		;MAXCLUS
	LD	(IX+15),H		;MAXCLUS

	;1セクタが512バイト以上の場合を考慮(2HD/1セクタ1024バイト/1.23MB等)
GETDPBD1:
	LD	A,(IX+3)		;SECSIZ HIGH
	AND	0FCH
	RET	Z

	SRL	(IX+3)			;SECSIZ HIGH

	SCF
	RL	(IX+6)			;CLUSMSK

	INC	(IX+7)			;CLUSSHFT

	SLA	(IX+8)			;FIRFAT LOW
	RL	(IX+9)			;FIRFAT HIGH

	SLA	(IX+16)			;FATSIZ

	SLA	(IX+17)			;FIRDIR
	RL	(IX+18)			;FIRDIR

	LD	L,(IX+17)		;FIRDIR
	LD	H,(IX+18)		;FIRDIR

	LD	A,2*4			;SECSIZ HIGH
	LD	E,(IX+11)		;MAXENT
GETDPBD5:
	SRL	E
	RRA
	JR	NC,GETDPBD5
	LD	D,0
	ADD	HL,DE
	LD	(IX+12),L		;FIRREC
	LD	(IX+13),H		;FIRREC

	JR	GETDPBD1

CHOICE:
	LD	HL,0
	RET

DSKFMT:
	XOR	A
	SCF
DSKSTP:
BASENT:
	RET

GETSLT:
	LD	A,(DRVTBL+1)
	RET

GET_DISK_BANK:
	OR	A		;A=DRIVE
	LD	A,DISK_BANK
	RET	Z
#if exists FIX_DRIVE
	LD	A,FIX_DRIVE
#else
	LD	A,(B_DRIVE)
#endif
	RET

DPB2DD:
	DB	0F9H	;MEDIA
	DW	00200H	;SECSIZ
	DB	00FH	;DIRMSK
	DB	004H	;DIRSHFT
	DB	001H	;CLUSMSK
	DB	002H	;CLUSSHFT
	DW	00001H	;FIRFAT
	DB	002H	;FATCNT
	DB	070H	;MAXENT
	DW	0000EH	;FIRREC
	DW	002CAH	;MAXCLUS
	DB	003H	;FATSIZ
	DW	0007H	;FIRDIR
DPB2DD_E:
